plugins {
    id 'com.github.hierynomus.license' version '0.11.0'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'net.researchgate.release' version '2.3.4'
}

group 'com.github.jonathanxd'
version '2.3.3-SNAPSHOT'


allprojects {
    apply plugin: "idea"
    apply plugin: 'java'
    apply plugin: 'license'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'net.researchgate.release'

    license {
        header rootProject.file("LICENSE_HEADER")
    }
}

defaultTasks 'licenseFormat', 'build', 'test', 'jar', 'shadowJar'

sourceCompatibility = 1.8

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
        }        
        resources {
            srcDirs 'src/main/resources'
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

// Tasks
allprojects {
    jar {
        from "$rootProject.rootDir/LICENSE"

        manifest {
            attributes 'Implementation-Title': baseName,
                    'Implementation-Version': version
        }
    }
}

/*

jar {
    from "LICENSE"

    manifest {
        attributes 'Implementation-Title': 'TextLexer',
                'Implementation-Version': version
    }
}*/
allprojects {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

subprojects {

    dependencies {
        compile rootProject
        testCompile rootProject.sourceSets.test.output
    }
}

allprojects{
    dependencies {
        compile 'com.github.JonathanxD:JwIUtils:2.6.3'
        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'
        testCompile group: 'junit', name:'junit', version: '4.11'
    }
}

allprojects{
    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

allprojects {
    test {
        useJUnit()
    }
}


license {
    ext.name = 'WCommands'
    ext.organization = 'TheRealBuggy/JonathanxD (https://github.com/JonathanxD/ & https://github.com/TheRealBuggy/)'
    ext.description = 'Yet Another Command API!'
    ext.url = 'https://github.com/JonathanxD/WCommands'
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.email = 'jonathan.scripter@programmer.net'

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"
    
    header rootProject.file('LICENSE_HEADER')
    
    sourceSets = project.sourceSets
    
    ignoreFailures false
    strictCheck true
    
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

release {
    preTagCommitMessage = 'Pre tag commit: '
    tagCommitMessage = 'Creating tag: '
    newVersionCommitMessage = 'Version Commit: '
}